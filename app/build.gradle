plugins {
    alias(libs.plugins.android.application)
}

// Place actual signing configuration in "keystore.properties"
// "keystore.properties" is in .gitignore and will not be checked into repo
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (!keystorePropertiesFile.exists()) {
    keystorePropertiesFile = rootProject.file("dummy_keystore.properties")
}
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'de.stephanlindauer.criticalmaps'

    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdk {
        version = release(36)
    }

    defaultConfig {
        applicationId "de.stephanlindauer.criticalmaps"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 50
        versionName "2.9.2"
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            pseudoLocalesEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/services/javax.annotation.processing.Processor']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding = true
        buildConfig true
    }

    lint {
        warning 'MissingTranslation', 'StringFormatInvalid', 'NewApi', 'InvalidPackage'
    }
}

dependencies {
    implementation libs.otto
    implementation libs.maplibre
    implementation libs.picasso
    implementation libs.timber
    implementation libs.okhttp
    implementation libs.typed.preferences
    implementation libs.material
    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    implementation libs.androidx.annotation
    implementation libs.androidx.exifinterface
    implementation libs.androidx.constraintlayout

    implementation libs.dagger
    annotationProcessor libs.dagger.compiler

    debugImplementation libs.leakcanary

    testImplementation libs.junit
    testImplementation libs.com.google.truth
    testImplementation libs.org.mockito.core

    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.espresso.core
}
