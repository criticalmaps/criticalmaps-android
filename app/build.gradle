apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'net.ltgt.errorprone'

// Place actual signing configuration in "keystore.properties"
// "keystore.properties" is in .gitignore and will not be checked into repo
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (!keystorePropertiesFile.exists()) {
    keystorePropertiesFile = rootProject.file("dummy_keystore.properties")
}
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 33

    defaultConfig {
        applicationId "de.stephanlindauer.criticalmaps"
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 42
        versionName "2.6.6"
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            pseudoLocalesEnabled true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/services/javax.annotation.processing.Processor']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding = true
    }

    lint {
        warning 'MissingTranslation', 'StringFormatInvalid', 'NewApi', 'InvalidPackage'
    }
    namespace 'de.stephanlindauer.criticalmaps'
}

dependencies {
    implementation 'com.squareup:otto:1.3.8'
    implementation 'org.osmdroid:osmdroid-android:6.1.8'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'com.google.android.material:material:1.9.0-alpha01'
    implementation 'androidx.exifinterface:exifinterface:1.3.5'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'com.squareup.okhttp3:okhttp:3.12.13'

    implementation 'info.metadude.android:typed-preferences:2.1.0'

    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    errorprone("com.google.errorprone:error_prone_core:2.18.0")

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'org.mockito:mockito-core:2.23.4'

    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
