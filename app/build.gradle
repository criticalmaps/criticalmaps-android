apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'net.ltgt.errorprone'

// Place actual signing configuration in "keystore.properties"
// "keystore.properties" is in .gitignore and will not be checked into repo
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (!keystorePropertiesFile.exists()) {
    keystorePropertiesFile = rootProject.file("dummy_keystore.properties")
}
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdk 36

    defaultConfig {
        applicationId "de.stephanlindauer.criticalmaps"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 49
        versionName "2.9.1"
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            pseudoLocalesEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/services/javax.annotation.processing.Processor']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding = true
        buildConfig true
    }

    lint {
        warning 'MissingTranslation', 'StringFormatInvalid', 'NewApi', 'InvalidPackage'
    }

    namespace 'de.stephanlindauer.criticalmaps'
}

dependencies {
    implementation 'com.squareup:otto:1.3.8'
    implementation 'org.osmdroid:osmdroid-android:6.1.8'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'androidx.core:core:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.exifinterface:exifinterface:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'com.squareup.okhttp3:okhttp:5.1.0'

    implementation 'info.metadude.android:typed-preferences:2.1.0'

    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    errorprone("com.google.errorprone:error_prone_core:2.18.0")

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'org.mockito:mockito-core:5.4.0'

    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // https://youtrack.jetbrains.com/issue/KT-54136/Duplicated-classes-cause-build-failure-if-a-dependency-to-kotlin-stdlib-specified-in-an-android-project#focus=Comments-27-6583109.0-0
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
